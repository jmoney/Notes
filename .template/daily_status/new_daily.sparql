BASE <https://www.jmoney.dev/notes/test>

PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX notes: <https://www.jmoney.dev/notes#>
PREFIX fn: <http://www.w3.org/2005/xpath-functions#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
    ?today_iri rdf:type notes:Daily .
    ?today_iri notes:date ?today .
    ?today_iri notes:previous ?daily .
    ?today_iri dcterms:isPartOf ?divider .
    ?today_iri notes:filename ?filename .
    ?today_iri notes:title ?today .

    ?daily notes:next ?today_iri .

    ?bnode rdf:type notes:Task .
    ?bnode dcterms:isPartOf ?today_iri .
    ?bnode notes:complete false .
    ?bnode notes:content ?task .
} WHERE {
    ?x rdf:type notes:Task .
    ?x dcterms:isPartOf ?daily .
    ?x notes:complete false .
    ?x notes:content ?task .
    ?daily notes:date ?date .
    ?daily dcterms:isPartOf ?divider .
    FILTER NOT EXISTS {
        ?daily notes:next ?next .
    }
    BIND("2023-07-13" AS ?today)
    BIND(REPLACE(?today, "-", "") AS ?today_key)
    BIND(CONCAT("#Daily", ?today_key) AS ?key)
    BIND(IRI(?key) AS ?today_iri)
    BIND(CONCAT("markdown/daily-status/", REPLACE(?today, "-", "_"), ".md") AS ?filename)
    BIND(BNODE() AS ?bnode)
}
